;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Roles
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role white)
(role black)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Initial State
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (num 1 available))
(init (num 2 available))
(init (num 3 available))
(init (num 4 available))
(init (num 5 available))
(init (num 6 available))
(init (num 7 available))
(init (num 8 available))
(init (num 9 available))
(init (control white))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Dynamic Components
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (num ?n ?p))
(does ?p (take ?n))
(true (num ?n available)))

(<= (next (num ?n ?c))
(true (num ?n ?c))
(distinct ?c available))

(<= (next (num ?n ?c))
(true (num ?n ?c))
(does ?p (take ?m))
(distinct ?m ?n))

(<= (next (control white))
(true (control black)))

(<= (next (control black))
(true (control white)))

(sum15 1 5 9)
(sum15 1 6 8)
(sum15 2 4 9)
(sum15 2 5 8)
(sum15 2 6 7)
(sum15 3 4 8)
(sum15 3 5 7)
(sum15 4 5 6)

(<= (has_sum15 ?p)
(role ?p)
(sum15 ?x ?y ?z)
(true (num ?x ?p))
(true (num ?y ?p))
(true (num ?z ?p)))

(<= open
(true (num ?n available)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?p (take ?n))
(true (num ?n available))
(true (control ?p)))

(<= (legal white noop)
(true (control black)))

(<= (legal black noop)
(true (control white)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal ?p 100)
(has_sum15 ?p))

(<= (goal ?p 50)
(role ?p)
(not (has_sum15 white))
(not (has_sum15 black))
(not open))

(<= (goal ?p 0)
(role ?p)
(role ?p2)
(distinct ?p ?p2)
(has_sum15 ?p2))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
(has_sum15 ?p))

(<= terminal
(not open))