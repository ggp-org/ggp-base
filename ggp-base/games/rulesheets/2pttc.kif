;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; TicTacChess (for two players)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role red)
(role blue)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (control red))

(init (cell 1 4 red))
(init (cell 7 4 blue))

(init (step 1))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?player noop)
    (role ?player))
    
(<= (legal ?player (move ?x1 ?y1 ?x2 ?y2))
	(true (control ?player))
	(true (cell ?x1 ?y1 ?player))
	(orthogonal ?x1 ?y1 ?x2 ?y2)
	(onBoard ?x2 ?y2)
	(true (cell ?x2 ?y2 ?player2))
	(distinct ?player ?player2))

(<= (legal ?player (move ?x1 ?y1 ?x2 ?y2))
	(true (control ?player))
	(true (cell ?x1 ?y1 ?player))
	(diagonal ?x1 ?y1 ?x2 ?y2)
	(onBoard ?x2 ?y2)
	(true (cell ?x2 ?y2 ?player2))
	(distinct ?player ?player2))
    
(<= (legal ?player (move ?x1 ?y1 ?x2 ?y2))
	(true (control ?player))
	(true (cell ?x1 ?y1 ?player))
	(ell ?x1 ?y1 ?x2 ?y2)
	(openCell ?x2 ?y2))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (control ?p2))
    (true (control ?p1))
    (rotation ?p1 ?p2))

(<= (next (cell ?x2 ?y2 ?player))
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))
(<= (next (cell ?x ?y ?piece))
    (true (cell ?x ?y ?piece))
    (not (cameFrom ?x ?y))
    (not (wentTo ?x ?y)))

(<= (next (cell ?x ?y ?player))
	(spawns ?x ?y ?player))

(<= (next (step ?t2))
	(true (step ?t1))
	(stepCount ?t1 ?t2))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
	(role ?player)
	(line ?player))
(<= terminal
	(true (step 31)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal ?player 0)
	(role ?player)
	(not (line ?player)))
(<= (goal ?player 100)
	(role ?player)
	(line ?player))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (orthogonal ?x1 ?y ?x2 ?y)
	(plus 1 ?x1 ?x2)
	(index ?y))
(<= (orthogonal ?x1 ?y ?x2 ?y)
	(plus 1 ?x2 ?x1)
	(index ?y))
(<= (orthogonal ?x ?y1 ?x ?y2)
	(index ?x)
	(plus 1 ?y1 ?y2))
(<= (orthogonal ?x ?y1 ?x ?y2)
	(index ?x)
	(plus 1 ?y2 ?y1))

(<= (diagonal ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x1 ?x2)
	(plus 1 ?y1 ?y2))
(<= (diagonal ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x1 ?x2)
	(plus 1 ?y2 ?y1))
(<= (diagonal ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x2 ?x1)
	(plus 1 ?y1 ?y2))
(<= (diagonal ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x2 ?x1)
	(plus 1 ?y2 ?y1))

(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 2 ?x1 ?x2)
	(plus 1 ?y1 ?y2))
(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 2 ?x1 ?x2)
	(plus 1 ?y2 ?y1))
(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 2 ?x2 ?x1)
	(plus 1 ?y1 ?y2))
(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 2 ?x2 ?x1)
	(plus 1 ?y2 ?y1))
(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x1 ?x2)
	(plus 2 ?y1 ?y2))
(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x1 ?x2)
	(plus 2 ?y2 ?y1))
(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x2 ?x1)
	(plus 2 ?y1 ?y2))
(<= (ell ?x1 ?y1 ?x2 ?y2)
	(plus 1 ?x2 ?x1)
	(plus 2 ?y2 ?y1))

(<= (openCell ?x ?y)
	(onBoard ?x ?y)
	(not (true (cell ?x ?y red)))
	(not (true (cell ?x ?y yellow)))
	(not (true (cell ?x ?y blue))))
	
(<= (onBoard ?x ?y)
	(index ?x)
	(index ?y)
	(distinct ?x 1)
	(distinct ?x 7)
	(distinct ?y 1)
	(distinct ?y 7))

(<= (cameFrom ?x1 ?y1)
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))
(<= (wentTo ?x2 ?y2)
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))

(<= (line ?player)
	(row ?player))
(<= (line ?player)
	(column ?player))
(<= (line ?player)
	(diag ?player))
	
(<= (row ?player)
	(true (cell 3 3 ?player))
	(true (cell 4 3 ?player))
	(true (cell 5 3 ?player)))
(<= (row ?player)
	(true (cell 3 4 ?player))
	(true (cell 4 4 ?player))
	(true (cell 5 4 ?player)))
(<= (row ?player)
	(true (cell 3 5 ?player))
	(true (cell 4 5 ?player))
	(true (cell 5 5 ?player)))
    
(<= (column ?player)
	(true (cell 3 3 ?player))
	(true (cell 3 4 ?player))
	(true (cell 3 5 ?player)))
(<= (column ?player)
	(true (cell 4 3 ?player))
	(true (cell 4 4 ?player))
	(true (cell 4 5 ?player)))
(<= (column ?player)
	(true (cell 5 3 ?player))
	(true (cell 5 4 ?player))
	(true (cell 5 5 ?player)))

(<= (diag ?player)
	(true (cell 3 3 ?player))
	(true (cell 4 4 ?player))
	(true (cell 5 5 ?player)))
(<= (diag ?player)
	(true (cell 3 5 ?player))
	(true (cell 4 4 ?player))
	(true (cell 5 3 ?player)))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3) (index 4) (index 5) (index 6) (index 7)

(plus 1 1 2) (plus 1 2 3) (plus 1 3 4) (plus 1 4 5) (plus 1 5 6) (plus 1 6 7)
(plus 2 1 3) (plus 2 2 4) (plus 2 3 5) (plus 2 4 6) (plus 2 5 7)

(rotation red blue) (rotation blue red)

(spawns 1 4 red) (spawns 7 4 blue)

(stepCount  1  2) (stepCount  2  3) (stepCount  3  4) (stepCount  4  5) (stepCount  5  6) (stepCount  6  7) (stepCount  7  8) (stepCount  8  9) (stepCount  9 10) (stepCount 10 11)
(stepCount 11 12) (stepCount 12 13) (stepCount 13 14) (stepCount 14 15) (stepCount 15 16) (stepCount 16 17) (stepCount 17 18) (stepCount 18 19) (stepCount 19 20) (stepCount 20 21)
(stepCount 21 22) (stepCount 22 23) (stepCount 23 24) (stepCount 24 25) (stepCount 25 26) (stepCount 26 27) (stepCount 27 28) (stepCount 28 29) (stepCount 29 30) (stepCount 30 31)