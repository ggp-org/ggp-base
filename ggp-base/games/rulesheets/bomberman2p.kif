;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; 2-Player Bomberman
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role bomberman)
(role bomberwoman)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (location bomberman   1 1))
(init (location bomberwoman 8 8))

(init (blockedNorth 2 1))
(init (blockedNorth 4 1))
(init (blockedNorth 5 1))
(init (blockedNorth 7 1))
(init (blockedNorth 2 2))
(init (blockedNorth 4 2))
(init (blockedNorth 5 2))
(init (blockedNorth 7 2))
(init (blockedNorth 2 3))
(init (blockedNorth 4 3))
(init (blockedNorth 5 3))
(init (blockedNorth 7 3))
(init (blockedNorth 2 5))
(init (blockedNorth 4 5))
(init (blockedNorth 5 5))
(init (blockedNorth 7 5))
(init (blockedNorth 2 6))
(init (blockedNorth 4 6))
(init (blockedNorth 5 6))
(init (blockedNorth 7 6))
(init (blockedNorth 2 7))
(init (blockedNorth 4 7))
(init (blockedNorth 5 7))
(init (blockedNorth 7 7))

(init (blockedNorth 1 8))
(init (blockedNorth 2 8))
(init (blockedNorth 3 8))
(init (blockedNorth 4 8))
(init (blockedNorth 5 8))
(init (blockedNorth 6 8))
(init (blockedNorth 7 8))
(init (blockedNorth 8 8))

(init (blockedEast 1 2))
(init (blockedEast 1 4))
(init (blockedEast 1 5))
(init (blockedEast 1 7))
(init (blockedEast 2 2))
(init (blockedEast 2 4))
(init (blockedEast 2 5))
(init (blockedEast 2 7))
(init (blockedEast 3 2))
(init (blockedEast 3 4))
(init (blockedEast 3 5))
(init (blockedEast 3 7))
(init (blockedEast 5 2))
(init (blockedEast 5 4))
(init (blockedEast 5 5))
(init (blockedEast 5 7))
(init (blockedEast 6 2))
(init (blockedEast 6 4))
(init (blockedEast 6 5))
(init (blockedEast 6 7))
(init (blockedEast 7 2))
(init (blockedEast 7 4))
(init (blockedEast 7 5))
(init (blockedEast 7 7))

(init (blockedEast 8 1))
(init (blockedEast 8 2))
(init (blockedEast 8 3))
(init (blockedEast 8 4))
(init (blockedEast 8 5))
(init (blockedEast 8 6))
(init (blockedEast 8 7))
(init (blockedEast 8 8))

(init (step 1))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?char (move ?dir))
    (role ?char)
	(true (location ?char ?x ?y))
	(legalStep ?dir ?x ?y))
(<= (legal ?char dropBomb)
    (role ?char))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (blockedNorth ?x ?y))
    (true (blockedNorth ?x ?y)))
(<= (next (blockedEast ?x ?y))
    (true (blockedEast ?x ?y)))

(<= (next (location ?char ?x2 ?y2))
    (role ?char)
    (true (location ?char ?x1 ?y1))
    (does ?char (move ?dir))
    (nextCell ?dir ?x1 ?y1 ?x2 ?y2))
(<= (next (location ?char ?x ?y))
    (role ?char)
    (true (location ?char ?x ?y))
    (does ?char dropBomb))

(<= (next (location bomb3 ?x ?y))
	(role ?char)
	(true (location ?char ?x ?y))
	(does ?char dropBomb))
(<= (next (location bomb2 ?x ?y))
    (true (location bomb3 ?x ?y)))
(<= (next (location bomb1 ?x ?y))
    (true (location bomb2 ?x ?y)))
(<= (next (location bomb0 ?x ?y))
    (true (location bomb1 ?x ?y)))

(<= (next (location fire ?xF ?y))
    (true (location bomb0 ?xB ?y))
    (not (true (blockedEast ?xB ?y)))
    (index ?xF))
(<= (next (location fire ?x ?yF))
    (true (location bomb0 ?x ?yB))
    (not (true (blockedNorth ?x ?yB)))
    (index ?yF))

(<= (next (step ?n++))
    (true (step ?n))
    (succ ?n ?n++))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
    bombermanBurned)
(<= terminal
    bomberwomanBurned)
(<= terminal
    timeout)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal bomberman 0)
    (not timeout)
    (not bombermanBurned)
    (not bomberwomanBurned))
(<= (goal bomberman 0)
    bombermanBurned
    (not bomberwomanBurned))
(<= (goal bomberman 50)
    bombermanBurned
    bomberwomanBurned)
(<= (goal bomberman 50)
    timeout
    (not bombermanBurned)
    (not bomberwomanBurned))
(<= (goal bomberman 100)
    (not bombermanBurned)
    bomberwomanBurned)
    
(<= (goal bomberwoman 0)    
    (not timeout)
    (not bombermanBurned)
    (not bomberwomanBurned))
(<= (goal bomberwoman 0)    
	(not bombermanBurned)
	bomberwomanBurned)
(<= (goal bomberwoman 50)
    bombermanBurned
    bomberwomanBurned)
(<= (goal bomberwoman 50)
    timeout
    (not bombermanBurned)
    (not bomberwomanBurned))
(<= (goal bomberwoman 100)
    bombermanBurned
    (not bomberwomanBurned))
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legalStep north ?x ?y)
    (++ ?y ?yNew)
    (cell ?x ?yNew)
    (not (blocked ?x ?y ?x ?yNew)))
(<= (legalStep south ?x ?y)
    (-- ?y ?yNew)
    (cell ?x ?yNew)
    (not (blocked ?x ?y ?x ?yNew)))
(<= (legalStep east ?x ?y)
    (++ ?x ?xNew)
    (cell ?xNew ?y)
    (not (blocked ?x ?y ?xNew ?y)))
(<= (legalStep west ?x ?y)
    (-- ?x ?xNew)
    (cell ?xNew ?y)
    (not (blocked ?x ?y ?xNew ?y)))
(<= (legalStep nowhere ?x ?y)
    (cell ?x ?y))

(<= (nextCell north ?x ?y ?x ?yNew)
    (index ?x)
    (++ ?y ?yNew))
(<= (nextCell south ?x ?y ?x ?yNew)
    (index ?x)
    (-- ?y ?yNew))
(<= (nextCell east ?x ?y ?xNew ?y)
    (index ?y)
    (++ ?x ?xNew))
(<= (nextCell west ?x ?y ?xNew ?y)
    (index ?y)
    (-- ?x ?xNew))
(<= (nextCell nowhere ?x ?y ?x ?y)
    (cell ?x ?y))
    
(<= (blocked ?x ?y1 ?x ?y2)
    (true (blockedNorth ?x ?y1))
    (++ ?y1 ?y2))
(<= (blocked ?x ?y2 ?x ?y1)
    (true (blockedNorth ?x ?y1))
    (++ ?y1 ?y2))
(<= (blocked ?x1 ?y ?x2 ?y)
    (true (blockedEast ?x1 ?y))
    (++ ?x1 ?x2))
(<= (blocked ?x2 ?y ?x1 ?y)
    (true (blockedEast ?x1 ?y))
    (++ ?x1 ?x2))
    
(<= (distinctCell ?x1 ?y1 ?x2 ?y2)
    (cell ?x1 ?y1)
	(cell ?x2 ?y2)
    (distinct ?x1 ?x2))
(<= (distinctCell ?x1 ?y1 ?x2 ?y2)
	(cell ?x1 ?y1)
	(cell ?x2 ?y2)
    (distinct ?y1 ?y2))
    
(<= bombermanBurned
    (true (location bomberman ?x ?y))
    (true (location fire ?x ?y)))
(<= bomberwomanBurned
    (true (location bomberwoman ?x ?y))
    (true (location fire ?x ?y)))

(<= timeout
    (true (step 50)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3) (index 4) (index 5) (index 6) (index 7) (index 8)

(cell 1 8) (cell 2 8) (cell 3 8) (cell 4 8) (cell 5 8) (cell 6 8) (cell 7 8) (cell 8 8)
(cell 1 7) (cell 2 7) (cell 3 7) (cell 4 7) (cell 5 7) (cell 6 7) (cell 7 7) (cell 8 7)
(cell 1 6) (cell 2 6) (cell 3 6) (cell 4 6) (cell 5 6) (cell 6 6) (cell 7 6) (cell 8 6)
(cell 1 5) (cell 2 5) (cell 3 5) (cell 4 5) (cell 5 5) (cell 6 5) (cell 7 5) (cell 8 5)
(cell 1 4) (cell 2 4) (cell 3 4) (cell 4 4) (cell 5 4) (cell 6 4) (cell 7 4) (cell 8 4)
(cell 1 3) (cell 2 3) (cell 3 3) (cell 4 3) (cell 5 3) (cell 6 3) (cell 7 3) (cell 8 3)
(cell 1 2) (cell 2 2) (cell 3 2) (cell 4 2) (cell 5 2) (cell 6 2) (cell 7 2) (cell 8 2)
(cell 1 1) (cell 2 1) (cell 3 1) (cell 4 1) (cell 5 1) (cell 6 1) (cell 7 1) (cell 8 1)

(++ 1 2) (++ 2 3) (++ 3 4) (++ 4 5) (++ 5 6) (++ 6 7) (++ 7 8)
(-- 8 7) (-- 7 6) (-- 6 5) (-- 5 4) (-- 4 3) (-- 3 2) (-- 2 1) 

(succ  1  2) (succ  2  3) (succ  3  4) (succ  4  5) (succ  5  6) 
(succ  6  7) (succ  7  8) (succ  8  9) (succ  9 10) (succ 10 11)
(succ 11 12) (succ 12 13) (succ 13 14) (succ 14 15) (succ 15 16)
(succ 16 17) (succ 17 18) (succ 18 19) (succ 19 20) (succ 20 21)
(succ 21 22) (succ 22 23) (succ 23 24) (succ 24 25) (succ 25 26)
(succ 26 27) (succ 27 28) (succ 28 29) (succ 29 30) (succ 30 31)
(succ 31 32) (succ 32 33) (succ 33 34) (succ 34 35) (succ 35 36)
(succ 36 37) (succ 37 38) (succ 38 39) (succ 39 40) (succ 40 41)
(succ 41 42) (succ 42 43) (succ 43 44) (succ 44 45) (succ 45 46)
(succ 46 47) (succ 47 48) (succ 48 49) (succ 49 50)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;