;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; LightsOn (Four games played in parallel)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLEs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role robot)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INITs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (cell 1 1 0))
(init (cell 1 2 0))
(init (cell 2 1 0))
(init (cell 2 2 0))

(init (cell 3 1 0))
(init (cell 3 2 0))
(init (cell 4 1 0))
(init (cell 4 2 0))

(init (cell 1 3 0))
(init (cell 1 4 0))
(init (cell 2 3 0))
(init (cell 2 4 0))

(init (cell 3 3 0))
(init (cell 3 4 0))
(init (cell 4 3 0))
(init (cell 4 4 0))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGALs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal robot (press ?x ?y))
	(index ?x)
	(index ?y))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXTs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (cell ?x ?y 4))
	(does robot (press ?x ?y)))

(<= (next (cell ?x ?y 3))
	(true (cell ?x ?y 4)))

(<= (next (cell ?x ?y 2))
	(true (cell ?x ?y 3)))

(<= (next (cell ?x ?y 1))
	(true (cell ?x ?y 2)))

(<= (next (cell ?x ?y 0))
	(true (cell ?x ?y 1)))

(<= (next (cell ?x ?y 0))
	(true (cell ?x ?y 0)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINALs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
    lightsOn-1)
(<= terminal
    lightsOn-2)
(<= terminal
    lightsOn-3)
(<= terminal
    lightsOn-4)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOALs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal robot 100)
	lightsOn-1)
(<= (goal robot 100)
	lightsOn-2)
(<= (goal robot 100)
	lightsOn-3)
(<= (goal robot 100)
	lightsOn-4)

(<= (goal robot 0)
    (not lightsOn-1)
    (not lightsOn-2)
    (not lightsOn-3)
    (not lightsOn-4))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Rules
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (on ?x ?y)
	(true (cell ?x ?y 1)))
(<= (on ?x ?y)
	(true (cell ?x ?y 2)))
(<= (on ?x ?y)
	(true (cell ?x ?y 3)))
(<= (on ?x ?y)
	(true (cell ?x ?y 4)))

(<= lightsOn-1
	(on 1 1)
	(on 1 2)
	(on 2 1)
	(on 2 2))

(<= lightsOn-2
	(on 3 1)
	(on 3 2)
	(on 4 1)
	(on 4 2))

(<= lightsOn-3
	(on 1 3)
	(on 1 4)
	(on 2 3)
	(on 2 4))

(<= lightsOn-4
	(on 3 3)
	(on 3 4)
	(on 4 3)
	(on 4 4))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Sentences
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3) (index 4)