;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Sudoku (by Sam Schreiber)
;;;
;;; Quick note: This description for Sudoku may be difficult for the theorem
;;; prover to handle, given the propositions with large numbers of variables.
;;; I haven't done testing recently enough to say for sure.
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role sudokuPlayer)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal sudokuPlayer (play ?i ?j ?m))
    (validMark ?m)
    (emptyCell ?i ?j))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (mark ?i ?j ?m))
    (does sudokuPlayer (play ?i ?j ?m)))
(<= (next (mark ?i ?j ?m))
    (true (mark ?i ?j ?m)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= emptyCellExists
	(emptyCell ?i ?j))

(<= terminal
    (not emptyCellExists))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal sudokuPlayer 100)
    victoryState)

(<= (goal sudokuPlayer 0)
    (not victoryState))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (exactlyFirst ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (eq ?z ?x1)
    (index ?x2)
    (index ?x3)
    (index ?x4)
    (index ?x5)
    (index ?x6)
    (index ?x7)
    (index ?x8)
    (index ?x9)
    (not (eq ?z ?x2))
    (not (eq ?z ?x3))
    (not (eq ?z ?x4))
    (not (eq ?z ?x5))
    (not (eq ?z ?x6))
    (not (eq ?z ?x7))
    (not (eq ?z ?x8))
    (not (eq ?z ?x9)))

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)) 

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x2 ?x1 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9))

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x3 ?x1 ?x2 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9))    

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x4 ?x1 ?x2 ?x3 ?x5 ?x6 ?x7 ?x8 ?x9))    

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x5 ?x1 ?x2 ?x3 ?x4 ?x6 ?x7 ?x8 ?x9))    

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x6 ?x1 ?x2 ?x3 ?x4 ?x5 ?x7 ?x8 ?x9))    

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x7 ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x8 ?x9))    

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x8 ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x9))    

(<= (exactlyOne ?z ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8 ?x9)
    (exactlyFirst ?z ?x9 ?x1 ?x2 ?x3 ?x4 ?x5 ?x6 ?x7 ?x8))    

(<= (goodRow ?i)
    (true (mark ?i 1 ?m1))
    (true (mark ?i 2 ?m2))
    (true (mark ?i 3 ?m3))
    (true (mark ?i 4 ?m4))
    (true (mark ?i 5 ?m5))
    (true (mark ?i 6 ?m6))
    (true (mark ?i 7 ?m7))
    (true (mark ?i 8 ?m8))
    (true (mark ?i 9 ?m9))
    (exactlyOne 1 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 2 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 3 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 4 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 5 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 6 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 7 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 8 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 9 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9))

(<= (goodColumn ?i)
    (true (mark 1 ?i ?m1))
    (true (mark 2 ?i ?m2))
    (true (mark 3 ?i ?m3))
    (true (mark 4 ?i ?m4))
    (true (mark 5 ?i ?m5))
    (true (mark 6 ?i ?m6))
    (true (mark 7 ?i ?m7))
    (true (mark 8 ?i ?m8))
    (true (mark 9 ?i ?m9))
    (exactlyOne 1 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 2 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 3 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 4 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 5 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 6 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 7 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 8 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 9 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9))

(<= (goodSquare ?i ?j)
    (succ ?i ?i2)
    (succ ?j ?j2)
    (succ ?i2 ?i3)    
    (succ ?j2 ?j3)
    (true (mark ?i ?j ?m1))
    (true (mark ?i ?j2 ?m2))
    (true (mark ?i ?j3 ?m3))
    (true (mark ?i2 ?j ?m4))
    (true (mark ?i2 ?j2 ?m5))
    (true (mark ?i2 ?j3 ?m6))
    (true (mark ?i3 ?j ?m7))
    (true (mark ?i3 ?j2 ?m8))
    (true (mark ?i3 ?j3 ?m9))   
    (exactlyOne 1 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 2 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 3 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 4 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 5 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 6 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 7 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 8 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9)
    (exactlyOne 9 ?m1 ?m2 ?m3 ?m4 ?m5 ?m6 ?m7 ?m8 ?m9))

(<= victoryState
    (goodRow 1)
    (goodRow 2)
    (goodRow 3)
    (goodRow 4)
    (goodRow 5)
    (goodRow 6)
    (goodRow 7)
    (goodRow 8)
    (goodRow 9)
    (goodColumn 1)
    (goodColumn 2)
    (goodColumn 3)
    (goodColumn 4)
    (goodColumn 5)
    (goodColumn 6)
    (goodColumn 7)
    (goodColumn 8)
    (goodColumn 9)
    (goodSquare 1 1)
    (goodSquare 1 4)
    (goodSquare 1 7)
    (goodSquare 4 1)
    (goodSquare 4 4)
    (goodSquare 4 7)
    (goodSquare 7 1)
    (goodSquare 7 4)
    (goodSquare 7 7))

(<= (emptyCell ?i ?j)
    (index ?i)
    (index ?j)
    (not (true (mark ?i ?j 1)))
    (not (true (mark ?i ?j 2)))
    (not (true (mark ?i ?j 3)))
    (not (true (mark ?i ?j 4)))
    (not (true (mark ?i ?j 5)))
    (not (true (mark ?i ?j 6)))
    (not (true (mark ?i ?j 7)))
    (not (true (mark ?i ?j 8)))
    (not (true (mark ?i ?j 9))))

(<= (validMark ?x)
    (index ?x))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3)
(index 4) (index 5) (index 6)
(index 7) (index 8) (index 9)

(eq 1 1) (eq 2 2) (eq 3 3)
(eq 4 4) (eq 5 5) (eq 6 6)
(eq 7 7) (eq 8 8) (eq 9 9)

(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)