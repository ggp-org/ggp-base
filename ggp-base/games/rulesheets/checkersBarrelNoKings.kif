;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; 
;;; Checkers.kif (Barrel rules - no kings)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role black)
(role red)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (location 1 1 blackPawn))
(init (location 3 1 blackPawn))
(init (location 5 1 blackPawn))
(init (location 7 1 blackPawn))
(init (location 2 2 blackPawn))
(init (location 4 2 blackPawn))
(init (location 6 2 blackPawn))
(init (location 8 2 blackPawn))
(init (location 1 3 blackPawn))
(init (location 3 3 blackPawn))
(init (location 5 3 blackPawn))
(init (location 7 3 blackPawn))

(init (location 2 6 redPawn))
(init (location 4 6 redPawn))
(init (location 6 6 redPawn))
(init (location 8 6 redPawn))
(init (location 1 7 redPawn))	
(init (location 3 7 redPawn))	
(init (location 5 7 redPawn))	
(init (location 7 7 redPawn))	
(init (location 2 8 redPawn))
(init (location 4 8 redPawn))
(init (location 6 8 redPawn))
(init (location 8 8 redPawn))

(init (captureCount black 0))	
(init (captureCount red 0))
	
(init (control black))	

(init (step 1))	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?player (move ?x1 ?y1 ?x2 ?y2))
    (true (control ?player))
	(not (hasJump ?player))
	(cellOccupiedBy ?x1 ?y1 ?player)
	(validMove ?x1 ?y1 ?x2 ?y2)
	(cellEmpty ?x2 ?y2))

(<= (legal ?player (jump ?x1 ?y1 ?x2 ?y2))
    (true (control ?player))
	(cellOccupiedBy ?x1 ?y1 ?player)
    (validJump ?x1 ?y1 ?x2 ?y2)
	(cellEmpty ?x2 ?y2))

(<= (legal ?player noop)
    (role ?player)
	(not (true (control ?player))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (location ?x2 ?y2 ?piece))
    (does black (move ?x1 ?y1 ?x2 ?y2))
	(true (location ?x1 ?y1 ?piece)))
(<= (next (location ?x2 ?y2 ?piece))
    (does black (jump ?x1 ?y1 ?x2 ?y2))
	(true (location ?x1 ?y1 ?piece)))
	
(<= (next (location ?x2 ?y2 ?piece))
    (does red (move ?x1 ?y1 ?x2 ?y2))
	(true (location ?x1 ?y1 ?piece)))
(<= (next (location ?x2 ?y2 ?piece))
    (does red (jump ?x1 ?y1 ?x2 ?y2))
	(true (location ?x1 ?y1 ?piece)))
	
(<= (next (location ?x ?y ?piece))
    (true (location ?x ?y ?piece))
	(does ?player (move ?x1 ?y1 ?x2 ?y2))
	(distinctCell ?x ?y ?x1 ?y1)
	(distinctCell ?x ?y ?x2 ?y2))
(<= (next (location ?x ?y ?piece))
    (true (location ?x ?y ?piece))
	(does ?player (jump ?x1 ?y1 ?x2 ?y2))
	(distinctCell ?x ?y ?x1 ?y1)
	(distinctCell ?x ?y ?x2 ?y2)
	(not (cellBetween ?x1 ?y1 ?x2 ?y2 ?x ?y)))

(<= (next (captureCount ?player ?count))
    (true (captureCount ?player ?count))
    (not (true (control ?player))))
(<= (next (captureCount ?player ?count))
    (true (captureCount ?player ?count))
    (does ?player (move ?x1 ?y1 ?x2 ?y2)))
(<= (next (captureCount ?player ?count2))
    (true (captureCount ?player ?count1))
    (does ?player (jump ?x1 ?y1 ?x2 ?y2))
    (countPlus ?count1 ?count2))

(<= (next (control black))
    (true (control red)))
(<= (next (control red))
    (true (control black)))
    
(<= (next (step ?tNext))
    (true (step ?tCurrent))
    (pp ?tCurrent ?tNext))
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
    (role ?player)
    (not (hasPiece ?player)))

(<= terminal
    (role ?player)
    (not (hasMove ?player))
    (not (hasJump ?player)))

(<= terminal
    (true (step 100))) 

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal ?player ?goal)
    (true (captureCount ?player ?count))
    (scoreMap ?count ?goal)) 

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (validMove ?x1 ?y1 ?x2 ?y2)
    (true (location ?x1 ?y1 blackPawn))
    (blackPawnMove ?x1 ?y1 ?x2 ?y2))
(<= (validMove ?x1 ?y1 ?x2 ?y2)
    (true (location ?x1 ?y1 redPawn))
    (redPawnMove ?x1 ?y1 ?x2 ?y2))

(<= (validJump ?x1 ?y1 ?x2 ?y2)
    (true (location ?x1 ?y1 blackPawn))
    (blackPawnJump ?x1 ?y1 ?x2 ?y2))
(<= (validJump ?x1 ?y1 ?x2 ?y2)
    (true (location ?x1 ?y1 redPawn))
    (redPawnJump ?x1 ?y1 ?x2 ?y2))

(<= (blackPawnMove ?x1 ?y1 ?x2 ?y2)
    (plusX 1 ?x1 ?x2)
    (plusY 1 ?y1 ?y2))
(<= (blackPawnMove ?x1 ?y1 ?x2 ?y2)
    (plusX 1 ?x2 ?x1)
    (plusY 1 ?y1 ?y2))

(<= (redPawnMove ?x1 ?y1 ?x2 ?y2)
    (plusX 1 ?x1 ?x2)
    (plusY 1 ?y2 ?y1))
(<= (redPawnMove ?x1 ?y1 ?x2 ?y2)
    (plusX 1 ?x2 ?x1)
    (plusY 1 ?y2 ?y1))
    
(<= (blackPawnJump ?x1 ?y1 ?x3 ?y3)
    (plusX 1 ?x1 ?x2)
    (plusX 1 ?x2 ?x3)
    (plusY 1 ?y1 ?y2)
    (plusY 1 ?y2 ?y3)
    (cellOccupiedBy ?x2 ?y2 red))
(<= (blackPawnJump ?x1 ?y1 ?x3 ?y3)
    (plusX 1 ?x3 ?x2)
    (plusX 1 ?x2 ?x1)
    (plusY 1 ?y1 ?y2)
    (plusY 1 ?y2 ?y3)
    (cellOccupiedBy ?x2 ?y2 red))

(<= (redPawnJump ?x1 ?y1 ?x3 ?y3)
    (plusX 1 ?x1 ?x2)
    (plusX 1 ?x2 ?x3)
    (plusY 1 ?y3 ?y2)
    (plusY 1 ?y2 ?y1)
    (cellOccupiedBy ?x2 ?y2 black))
(<= (redPawnJump ?x1 ?y1 ?x3 ?y3)
    (plusX 1 ?x3 ?x2)
    (plusX 1 ?x2 ?x1)
    (plusY 1 ?y3 ?y2)
    (plusY 1 ?y2 ?y1)
    (cellOccupiedBy ?x2 ?y2 black))

(<= (hasPiece ?player)
    (true (location ?x ?y ?piece))
    (owns ?piece ?player))
    
(<= (hasJump ?player)
	(cellOccupiedBy ?x1 ?y1 ?player)
    (validJump ?x1 ?y1 ?x2 ?y2)
	(cellEmpty ?x2 ?y2))

(<= (hasMove ?player)
	(cellOccupiedBy ?x1 ?y1 ?player)
	(validMove ?x1 ?y1 ?x2 ?y2)
	(cellEmpty ?x2 ?y2))

(<= (distinctCell ?x1 ?y1 ?x2 ?y2)
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (distinct ?x1 ?x2))
(<= (distinctCell ?x1 ?y1 ?x2 ?y2)
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (distinct ?y1 ?y2))

(<= (cellBetween ?x1 ?y1 ?x3 ?y3 ?x2 ?y2)
    (between ?x1 ?x2 ?x3)
    (between ?y1 ?y2 ?y3))

(<= (cellOccupiedBy ?x ?y ?player)
    (true (location ?x ?y ?piece))
    (owns ?piece ?player))	

(<= (cellEmpty ?x ?y)
    (cell ?x ?y)
    (not (cellOccupiedBy ?x ?y black))
    (not (cellOccupiedBy ?x ?y red)))

(<= (cell ?x ?y)
    (index ?x)
    (index ?y))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Relations 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3) (index 4) (index 5) (index 6) (index 7) (index 8)
	
(plusX 1 1 2) (plusX 1 2 3) (plusX 1 3 4) (plusX 1 4 5) (plusX 1 5 6) (plusX 1 6 7) (plusX 1 7 8)
(plusY 1 1 2) (plusY 1 2 3) (plusY 1 3 4) (plusY 1 4 5) (plusY 1 5 6) (plusY 1 6 7) (plusY 1 7 8) (plusY 1 8 1)

(between 1 2 3) (between 2 3 4) (between 3 4 5) (between 4 5 6) (between 5 6 7) (between 6 7 8) (between 7 8 1) (between 8 1 2)
(between 3 2 1) (between 4 3 2) (between 5 4 3) (between 6 5 4) (between 7 6 5) (between 8 7 6) (between 1 8 7) (between 2 1 8)

(countPlus  0  1) (countPlus  1  2) (countPlus  2  3) (countPlus  3  4) 
(countPlus  4  5) (countPlus  5  6) (countPlus  6  7) (countPlus  7  8)
(countPlus  8  9) (countPlus  9 10) (countPlus 10 11) (countPlus 11 12)

           (pp  1  2) (pp  2  3) (pp  3  4) (pp  4  5) (pp  5  6) (pp  6  7) (pp  7  8) (pp  8  9) (pp  9  10)
(pp 10 11) (pp 11 12) (pp 12 13) (pp 13 14) (pp 14 15) (pp 15 16) (pp 16 17) (pp 17 18) (pp 18 19) (pp 19  20)	
(pp 20 21) (pp 21 22) (pp 22 23) (pp 23 24) (pp 24 25) (pp 25 26) (pp 26 27) (pp 27 28) (pp 28 29) (pp 29  30)	
(pp 30 31) (pp 31 32) (pp 32 33) (pp 33 34) (pp 34 35) (pp 35 36) (pp 36 37) (pp 37 38) (pp 38 39) (pp 39  40)	
(pp 40 41) (pp 41 42) (pp 42 43) (pp 43 44) (pp 44 45) (pp 45 46) (pp 46 47) (pp 47 48) (pp 48 49) (pp 49  50)	
(pp 50 51) (pp 51 52) (pp 52 53) (pp 53 54) (pp 54 55) (pp 55 56) (pp 56 57) (pp 57 58) (pp 58 59) (pp 59  60)	
(pp 60 61) (pp 61 62) (pp 62 63) (pp 63 64) (pp 64 65) (pp 65 66) (pp 66 67) (pp 67 68) (pp 68 69) (pp 69  70)	
(pp 70 71) (pp 71 72) (pp 72 73) (pp 73 74) (pp 74 75) (pp 75 76) (pp 76 77) (pp 77 78) (pp 78 79) (pp 79  80)	
(pp 80 81) (pp 81 82) (pp 82 83) (pp 83 84) (pp 84 85) (pp 85 86) (pp 86 87) (pp 87 88) (pp 88 89) (pp 89  90)	
(pp 90 91) (pp 91 92) (pp 92 93) (pp 93 94) (pp 94 95) (pp 95 96) (pp 96 97) (pp 97 98) (pp 98 99) (pp 99 100)		
	
(scoreMap  0    0)
(scoreMap  1    8)
(scoreMap  2   16)
(scoreMap  3   24)
(scoreMap  4   32)
(scoreMap  5   40)
(scoreMap  6   48)
(scoreMap  7   56)
(scoreMap  8   64)
(scoreMap  9   72)
(scoreMap 10   80)
(scoreMap 11   90)
(scoreMap 12  100)

(owns blackPawn black)
(owns redPawn red)